buildscript {
  repositories {
    mavenCentral()
    google()
  }

  ext.versions = [
      "dagger": "2.18",
      "retrofit": "2.4.0",
  ]

  ext.deps = [
      "androidx": [
          "annotations": "androidx.annotation:annotation:1.0.0",
          "appCompat": "androidx.appcompat:appcompat:1.0.0",
          "constraintLayout": "androidx.constraintlayout:constraintlayout:1.1.3",
          "recyclerView": "androidx.recyclerview:recyclerview:1.0.0",
      ],
      "dagger": [
          "runtime": "com.google.dagger:dagger:${versions.dagger}",
          "compiler": "com.google.dagger:dagger-compiler:${versions.dagger}",
          "androidRuntime": "com.google.dagger:dagger-android:${versions.dagger}",
          "androidCompiler": "com.google.dagger:dagger-android-processor:${versions.dagger}",
      ],
      "okio": "com.squareup.okio:okio:2.1.0",
      "okhttp": "com.squareup.okhttp3:okhttp:3.11.0",
      "moshi": "com.squareup.moshi:moshi:1.7.0",
      "rx": [
          "java": "io.reactivex.rxjava2:rxjava:2.2.3",
          "android": "io.reactivex.rxjava2:rxandroid:2.1.0",
          "binding": "com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha1",
      ],
      "retrofit": [
          "client": "com.squareup.retrofit2:retrofit:${versions.retrofit}",
          "converterMoshi": "com.squareup.retrofit2:converter-moshi:${versions.retrofit}",
          "adapterRxJava": "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
      ],
      "picasso": "com.squareup.picasso:picasso:2.71828",
      "junit": "junit:junit:4.12",
      "assertj": "org.assertj:assertj-core:3.11.1",
  ]

  dependencies {
    classpath "com.android.tools.build:gradle:3.2.1"
  }
}

apply plugin: "com.android.application"

repositories {
  mavenCentral()
  google()
}

android {
  compileSdkVersion 28
  buildToolsVersion "28.0.3"

  defaultConfig {
    applicationId "io.mehow.luckystrike"

    minSdkVersion 24
    targetSdkVersion 28

    versionCode 1
    versionName "0.0.1"

    resConfigs "en"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation deps.androidx.annotations
  implementation deps.androidx.appCompat
  implementation deps.androidx.constraintLayout
  implementation deps.androidx.recyclerView

  implementation deps.dagger.runtime
  implementation deps.dagger.androidRuntime
  annotationProcessor deps.dagger.compiler
  annotationProcessor deps.dagger.androidCompiler

  implementation deps.rx.java
  implementation deps.rx.android
  implementation deps.rx.binding

  implementation deps.okio
  implementation deps.okhttp
  implementation deps.moshi
  implementation deps.retrofit.client
  implementation deps.retrofit.converterMoshi
  implementation deps.retrofit.adapterRxJava

  implementation deps.picasso

  testImplementation deps.junit
  testImplementation deps.assertj
}
